buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {

        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:4.4.1"

    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
//subprojects {
//    project.evaluationDependsOn(':app')
//}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android.compileSdkVersion = 35
            // Auto-configure namespace for packages that don't have one
            if (project.android.namespace == null) {
                def manifest = project.file("src/main/AndroidManifest.xml")
                if (manifest.exists()) {
                    def manifestText = manifest.text
                    def packageMatch = manifestText =~ /package="([^"]+)"/
                    if (packageMatch) {
                        project.android.namespace = packageMatch[0][1]
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
